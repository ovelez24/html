'Pegar en un módulo normal

'Declarar las variables a nivel del módulo
Dim AppPP As Object
Dim PresentacionPP As Object
Dim DiapoPP As Object
Dim NombrePresentador As String
Dim CM As Single
Dim Pulgada As Integer
Dim X As Integer


Private Sub ElPedido()

CrearPresentacionPP
Diapo1
Diapo2
Diapo3
Diapo4
Diapo5
DestruirVariables

End Sub

Private Sub AgregarLogo()

    'Agrega una imagen guardada
    AppPP.ActivePresentation.Slides(X).Shapes.AddPicture _
        "C:\Users\usuario\Pictures\Logo.jpg", _
            LinkToFile:=msoFalse, _
                SaveWithDocument:=msoTrue, _
                    Left:=30.36 * CM, Top:=0.68 * CM, Width:=2.52 * CM, Height:=2.52 * CM

End Sub

Private Sub Diapo5()

    'Establecer número de diapositiva
    X = 5
    
    'Agregar diapositiva (título y subtítulo)
    Set DiapoPP = PresentacionPP.Slides.Add(Index:=X, Layout:=1)

    AgregarLogo
    
    'Agregar título
    DiapoPP.Shapes(1).TextFrame.TextRange.Text = "Gracias y..." & vbNewLine & "¿alguna pregunta?"
    
    With DiapoPP.Shapes(1)
        .TextFrame.TextRange.Font.Color = vbBlue
        '.TextFrame.TextRange.Font.Size = 24
        .TextFrame.TextRange.Font.Bold = True
    End With
    
    'Agregar subtítulo
    DiapoPP.Shapes(2).Delete


End Sub


Private Sub Diapo4()

    'Establecer número de diapositiva
    X = 4
    
    'Agregar diapositiva (título y subtítulo)
    Set DiapoPP = PresentacionPP.Slides.Add(Index:=X, Layout:=8)
    
    AgregarLogo
    
    'Agregar título
    DiapoPP.Shapes(1).TextFrame.TextRange.Text = "Informe de Desempeño"
    
    With DiapoPP.Shapes(1)
        .TextFrame.TextRange.Font.Color = vbBlue
        '.TextFrame.TextRange.Font.Size = 24
        .TextFrame.TextRange.Font.Bold = True
    End With
    
    'Agregar primera tabla
    Range("A23").CurrentRegion.CopyPicture Appearance:=xlScreen, Format:=xlPicture
    DiapoPP.Shapes.Paste '(se pega en el centro por defecto)
    
    'Alinear la imagen usando puntos
    AppPP.ActiveWindow.Selection.ShapeRange.Left = 2.33 * CM
    AppPP.ActiveWindow.Selection.ShapeRange.Top = 5.13 * CM
    
    'Agregar segunda tabla
    Range("D23").CurrentRegion.CopyPicture Appearance:=xlScreen, Format:=xlPicture
    DiapoPP.Shapes.Paste '(se pega en el centro por defecto)
    
    'Alinear la imagen usando puntos
    AppPP.ActiveWindow.Selection.ShapeRange.Left = 2.33 * CM
    AppPP.ActiveWindow.Selection.ShapeRange.Top = 12.95 * CM
    
    'Pegar el gráfico usando bitmap
    Sheets("Hoja1").ChartObjects("GraficoTorta").Chart.CopyPicture
    DiapoPP.Shapes.Paste '(se pega en el centro por defecto)
    
    AppPP.ActiveWindow.Selection.ShapeRange.Height = 12.69 * CM
    AppPP.ActiveWindow.Selection.ShapeRange.Left = 15.1 * CM
    AppPP.ActiveWindow.Selection.ShapeRange.Top = 5.13 * CM
    
    
    'Eliminar la caja de texto
    DiapoPP.Shapes(2).Delete

End Sub



Private Sub Diapo3()

    'Establecer número de diapositiva
    X = 3
    
    'Agregar diapositiva (título y subtítulo)
    Set DiapoPP = PresentacionPP.Slides.Add(Index:=X, Layout:=8)

    AgregarLogo
    
    'Agregar título
    DiapoPP.Shapes(1).TextFrame.TextRange.Text = "Ganancia por Día"
    
    With DiapoPP.Shapes(1)
        .TextFrame.TextRange.Font.Color = vbBlue
        '.TextFrame.TextRange.Font.Size = 24
        .TextFrame.TextRange.Font.Bold = True
    End With
    
    'Pegar gráfico como bitmap
    Sheets("Hoja1").ChartObjects("GananciaPorDiaGrafico").Chart.CopyPicture
    DiapoPP.Shapes.Paste '(se pega en el centro por defecto)
    
    AppPP.ActiveWindow.Selection.ShapeRange.Height = 11.65 * CM
    AppPP.ActiveWindow.Selection.ShapeRange.Align msoAlignCenters, True
    
    
    'Eliminar la caja de texto
    DiapoPP.Shapes(2).Delete

End Sub




Private Sub Diapo2()

    'Establecer número de diapositiva
    X = 2
    
    'Agregar diapositiva (título y subtítulo)
    Set DiapoPP = PresentacionPP.Slides.Add(Index:=X, Layout:=2)

    AgregarLogo
    
    'Agregar título
    DiapoPP.Shapes(1).TextFrame.TextRange.Text = "Registro de Desempeño"
    
    With DiapoPP.Shapes(1)
        .TextFrame.TextRange.Font.Color = vbBlue
        '.TextFrame.TextRange.Font.Size = 24
        .TextFrame.TextRange.Font.Bold = True
    End With
    
    'Agregar subtítulo
    DiapoPP.Shapes(2).TextFrame.TextRange.Text = _
        Range("A3").Value & vbNewLine & _
        Range("A4").Value & vbNewLine & _
        Range("A5").Value


End Sub


Private Sub Diapo1()

    'Establecer número de diapositiva
    X = 1
    
    'Agregar diapositiva (título y subtítulo)
    Set DiapoPP = PresentacionPP.Slides.Add(Index:=X, Layout:=1)

    AgregarLogo
    
    'Agregar título
    DiapoPP.Shapes(1).TextFrame.TextRange.Text = Range("A1").Value
    
    With DiapoPP.Shapes(1)
        .TextFrame.TextRange.Font.Color = vbBlue
        '.TextFrame.TextRange.Font.Size = 24
        .TextFrame.TextRange.Font.Bold = True
    End With
    
    'Agregar subtítulos
    DiapoPP.Shapes(2).TextFrame.TextRange.Text = "Presentado por: " & NombrePresentador


End Sub


Private Sub CrearPresentacionPP()

    CM = 28.346
    Pulgada = 72

    'Seleccionar la hoja correcta
    Hoja2.Select

    'Capturar el nombre del presentador
    NombrePresentador = InputBox("Por favor ingrese el nombre del presentador aquí:" _
                        , "¿Quién es el presentador?")
    
    'Crear (abrir) la aplicación PowerPoint
    Set AppPP = CreateObject("PowerPoint.Application")

    'agregar una nueva presentación
    Set PresentacionPP = AppPP.Presentations.Add
    
    'Hacer que PowerPoint esté visible (no activo)
    AppPP.Visible = True
    
End Sub


Private Sub DestruirVariables()
 
    'Destruir las variables objeto (en orden inverso)
    Set DiapoPP = Nothing
    Set PresentacionPP = Nothing
    Set AppPP = Nothing
    
End Sub

